// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux4Way16(a=x[0..15], b[0..15]=false, sel[0]=zx, out[0..15]=xOrZeroX);
    Mux4Way16(a=y[0..15], b[0..15]=false, sel[0]=zy, out[0..15]=yOrZeroY);

    Not16(in=xOrZeroX, out=invertedX);
    Not16(in=yOrZeroY, out=invertedY);

    Mux4Way16(a=xOrZeroX, b=invertedX, sel[0]=nx, out=xOrInvertedX);
    Mux4Way16(a=yOrZeroY, b=invertedY, sel[0]=ny, out=yOrInvertedY);

    Add16(a=xOrInvertedX, b=yOrInvertedY, out=xPlusY);
    And16(a=xOrInvertedX, b=yOrInvertedY, out=xAndY);

    Mux4Way16(a=xAndY, b=xPlusY, sel[0]=f, out=opertaionResult);
    Not16(in=opertaionResult, out=invertedResult);

    Mux4Way16(a=opertaionResult, b=invertedResult, sel[0]=no, out=result, out=out, out[15]=ng);
    IsZero16(in=result, out=zr);
}